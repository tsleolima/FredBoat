allprojects {
    repositories {
        mavenCentral()                     // main maven repo
        jcenter()                          // JDA and some other stuff
        maven { url 'https://dl.bintray.com/napster/SqlSauce' }
        mavenLocal()                       // useful for developing
        maven { url "https://jitpack.io" } // build projects directly from github
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }

    apply plugin: 'idea'
    group = 'com.fredboat'
}

subprojects {
    buildscript {
        ext {
            //@formatter:off

            //plugin versions
            gradleGitVersion        = '1.4.17'          //do not upgrade to 1.4.20, it fails builds randomly
            jdactionVersion         = '1.0.2'
            shadowVersion           = '2.0.2'
            springBootVersion       = '2.0.0.RC1'
            jUnitPluginVersion      = '1.0.2'
            propDepsVersion         = '0.0.9.RELEASE'

            //@formatter:on
        }
        repositories {
            maven { url "https://plugins.gradle.org/m2/" }
            maven { url 'http://repo.spring.io/plugins-release' }
            maven { url "https://repo.spring.io/snapshot" }
            maven { url "https://repo.spring.io/milestone" }
            mavenCentral()
        }
        dependencies {
            classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitVersion}"
            classpath "gradle.plugin.com.sedmelluq:jdaction:${jdactionVersion}"
            classpath "com.github.jengelman.gradle.plugins:shadow:${shadowVersion}"
            classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
            classpath "org.junit.platform:junit-platform-gradle-plugin:${jUnitPluginVersion}"
            classpath "io.spring.gradle:propdeps-plugin:${propDepsVersion}"
        }
    }
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.dependsOn 'clean'
    compileJava.options.encoding = 'UTF-8'
    compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    ext {
        //@formatter:off

        springBootVersion           = "${springBootVersion}"
        //discord deps
        jdaVersion                  = '3.5.1_339'

        //audio deps
        lavaplayerVersion           = '1.2.53'
        jdaNasVersion               = '1.0.6'
        lavalinkVersion             = '2f0df387c36b1850f63779398e89270c83f56ee3'

        //utility deps
        jsonOrgVersion              = '20180130'
        commonsTextVersion          = '1.2'
        commonsIOVersion            = '2.6'
        commonsCodecVersion         = '1.11'
        commonsCollectionsVersion   = '4.1'
        snakeYamlVersion            = '1.19'
        fastUtilVersion             = '8.1.1'
        togglzVersion               = '2.5.0.Final'
        guavaVersion                = '24.0-jre'
        bucket4jVersion             = '3.1.1'

        //logging / monitoring deps
        logbackVersion              = '1.2.3'
        sentryVersion               = '1.6.8'
        prometheusClientVersion     = '0.2.0'

        //http / web deps
        okhttpVersion               = '3.9.1'
        sparkVersion                = '2.7.1'

        //QA deps
        spotbugsVersion             = '3.1.1'
        napsterAnnotations          = '0.0.1'

        //database deps
        sqlsauceVersion             = '0.0.11'
        hibernateVersion            = '5.2.13.Final'
        flywayVersion               = '5.0.7'
        dsProxyVersion              = '1.4.6'

        //testing deps
        junitVersion                = '5.0.3'
        apiGuardianVersion          = '1.0.0'


        //@formatter:on
    }
}

version = '3.0'
ext {
    moduleName = 'FredBoat-Root'
}

import org.gradle.api.tasks.wrapper.Wrapper.DistributionType

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
    distributionType = DistributionType.ALL
}

//create a simple version file that we will be reading to create appropriate docker tags
void versionTxt() {
    new File("$projectDir/VERSION.txt").text = "$rootProject.version\n"
}

versionTxt()
